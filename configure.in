#######################################################################
#	Thus it begins...
#######################################################################

AC_INIT(LibSnert, 1.75, [Anthony Howe <achowe@snert.com>])

dnl The autoconf version I learned to deal with.
AC_PREREQ(2.59)

SNERT_INIT(LIBSNERT, [Copyright 1996, 2014 by Anthony Howe. All rights reserved.])

AC_DEFINE_UNQUOTED(LIBSNERT_UNAME, [["${platform}"]])

#######################################################################
#	Setup paths
#######################################################################

# The total perspective vortex "You are here..."
AC_CONFIG_SRCDIR($PACKAGE_NAME.c)

abs_tardir=`pwd | sed -e 's,/cygdrive/\(.\),\1:,' -e 's,^\(.*\)/com/snert/src/.*,\1,'`
AC_SUBST(abs_incdir, $abs_tardir/com/snert/include)
AC_SUBST(abs_libdir, $abs_tardir/com/snert/lib)
AC_SUBST(abs_tardir, $abs_tardir)

AC_SUBST(snert_tardir, '${top_srcdir}/../../../..')
AC_SUBST(snert_incdir, '${top_srcdir}/../../include')
AC_SUBST(snert_libdir, '${top_srcdir}/../../lib')

CFLAGS="-I$snert_incdir $CFLAGS"
LDFLAGS="-L$snert_libdir $LDFLAGS"

#######################################################################
#	Auxliary files to be modified later
#######################################################################

AC_CONFIG_HEADERS([include/version.h:version.h.in])

AC_CONFIG_FILES(version.h.in)
AC_CONFIG_FILES(version.sh)
AC_CONFIG_FILES(libsnert.pc)
AC_CONFIG_FILES(makefile)
AC_CONFIG_FILES(crc/makefile)
AC_CONFIG_FILES(object/makefile)
AC_CONFIG_FILES(type/makefile)
AC_CONFIG_FILES(util/makefile)
AC_CONFIG_FILES(mail/makefile)
AC_CONFIG_FILES(net/makefile)
AC_CONFIG_FILES(sys/makefile)
AC_CONFIG_FILES(io/makefile)
AC_CONFIG_FILES(tools/makefile)

#######################################################################
#	Configuration options: settings
#######################################################################

SNERT_OPTION_ENABLE_64BIT

AC_ARG_ENABLE(access-tagless,
	[AC_HELP_STRING([--enable-access-tagless],[support for tagless access records (Sendmail, Postfix)])],
	[
		AC_DEFINE_UNQUOTED(ENABLE_ACCESS_TAGLESS)
		AC_SUBST(enable_access_tagless)
	]
)

AC_ARG_ENABLE(cache-db-185,
	[AC_HELP_STRING([--enable-cache-db-185],[try cache with Berkeley DB 1.85 (unsupported)])],
	[AC_DEFINE_UNQUOTED(IGNORE_CORRUPT_CACHE_ISSUE_WITH_DB_185)]
)

SNERT_OPTION_ENABLE_DEBUG

AC_ARG_ENABLE(debug-mutex,
	[AC_HELP_STRING([--enable-debug-mutex],[enable lockpick mutex debug functions])],
	[AC_DEFINE_UNQUOTED(DEBUG_MUTEX, [$enable_debug_mutex], [Enable application mutex debugging; set to 2 to include library hooking.])]
)

SNERT_OPTION_ENABLE_FORK
SNERT_OPTION_ENABLE_FCNTL_LOCKS
SNERT_OPTION_ENABLE_MINGW
SNERT_OPTION_ENABLE_POPAUTH

#######################################################################
#	Configuration options: packages
#######################################################################

SNERT_OPTION_WITH_DB
SNERT_OPTION_WITH_LIBEV
SNERT_OPTION_WITH_LUA
SNERT_OPTION_WITH_MILTER
SNERT_OPTION_WITH_OPENSSL
SNERT_OPTION_WITH_PTHREAD
SNERT_OPTION_WITH_SQLITE3
SNERT_BUILD_THREADED_SQLITE3
SNERT_OPTION_WITH_WINDOWS_SDK

AC_ARG_WITH(mutex,
	[AC_HELP_STRING([--with-mutex=API],[where API is fcntl, flock, posix, systemv, win32])], [
		if test "$withval" = "fcntl"; then
			with_mutex='FCNTL_API'
		elif test "$withval" = "flock"; then
			with_mutex='FLOCK_API'
		elif test "$withval" = "posix"; then
			with_mutex='POSIX_API'
		elif test "$withval" = "systemv"; then
			with_mutex='SYSTEMV_API'
		elif test "$withval" = "win32"; then
			with_mutex='WIN32_API'
		else
			with_mutex='UNKNOWN_API'
		fi
	],[
		with_mutex='UNKNOWN_API'
	]
)

AC_ARG_WITH(shar-mem,
	[AC_HELP_STRING([--with-shar-mem=API],[where API is malloc, mmap-anon, posix, systemv, squeeze])], [
		if test "$withval" = "malloc"; then
			with_shar_mem='MALLOC_API'
		elif test "$withval" = "mmap-anon"; then
			with_shar_mem='MMAP_ANON'
		elif test "$withval" = "posix"; then
			with_shar_mem='POSIX_API'
		elif test "$withval" = "systemv"; then
			with_shar_mem='SYSTEMV_API'
		elif test "$withval" = "squeeze"; then
			echo
			echo "Oi! Don't squeeze the shar-mem! :>"
			echo
			with_shar_mem='UNKNOWN_API'
		else
			with_shar_mem='UNKNOWN_API'
		fi
	],[
		with_shar_mem='UNKNOWN_API'
	]
)

#######################################################################
#	Check for programs
#
#	Must do this AFTER setting up Borland specifc changes that
#	AC_PROG_CC won't detect otherwise.
#######################################################################

SNERT_DEFAULT_CC_SETTINGS
SNERT_TAR_SETTINGS

AC_PROG_MAKE_SET

AC_C_CONST
AC_CHECK_TYPES([mode_t, off_t, pid_t, uid_t, gid_t, size_t, ssize_t, time_t, intptr_t, uintptr_t])

#######################################################################
#	Check for library, header, and function families.
#######################################################################

SNERT_CHECK_PREDEFINE([__WIN32__])
SNERT_FIND_LIBC
SNERT_BACKTRACE
SNERT_ANSI_TIME
SNERT_PROCESS
SNERT_POSIX_IO
SNERT_POSIX_SIGNALS
SNERT_POSIX_SEMAPHORES
SNERT_TERMIOS
SNERT_PTHREAD
SNERT_HASHES
SNERT_OPENSSL
SNERT_SQLITE3
SNERT_LIBEV
SNERT_LUA
SNERT_BERKELEY_DB
SNERT_SYSTEMV_SEMAPHORES
SNERT_NETWORK
SNERT_SYS
SNERT_PAM
SNERT_SETJMP
SNERT_RANDOM
SNERT_EXTRA_STDIO
SNERT_ANSI_STRING
SNERT_EXTRA_STRING
SNERT_REGEX
SNERT_LIBMILTER

echo
echo 'Miscellanous header & function checks...'
echo

AC_CHECK_HEADERS([sys/mman.h sys/shm.h])
AC_CHECK_HEADERS([ctime.h grp.h pwd.h sys/file.h])
AC_CHECK_FUNCS([flockfile funlockfile])

#######################################################################
#	Check for library functions
#######################################################################

AC_FUNC_MALLOC
AC_LIBOBJ(malloc)

AC_FUNC_MMAP
AC_FUNC_SETVBUF_REVERSED

AC_CHECK_FUNCS(brk sbrk)
AC_CHECK_FUNCS(atexit getenv putenv toupper tolower)

dnl SNERT_FUNC_FLOCK
AC_CHECK_FUNCS(flock fcntl lockf locking)
SNERT_CHECK_DEFINE(O_BINARY, fcntl.h)
SNERT_CHECK_DEFINE(LOCK_SH, sys/file.h)
if test $ac_cv_define_LOCK_SH = 'yes'; then
	AC_DEFINE_UNQUOTED(HAVE_LOCK_SH)
fi

#######################################################################
#	Determine avaliable Mutex APIs
#######################################################################

has_mutex_fcntl_api='yes'
AC_CHECK_FUNCS(fcntl, [], [has_mutex_fcntl_api='no'])

has_mutex_flock_api='yes'
AC_CHECK_FUNCS(flock, [], [has_mutex_flock_api='no'])

if test "$ac_cv_header_windows_h" = 'yes'; then
	has_mutex_win32_api='yes'
else
	has_mutex_win32_api='no'
fi

AC_MSG_CHECKING([which mutex api to use])

if test "$with_mutex" = 'UNKNOWN_API'; then
	# Out in left field...
	if test "$ac_cv_define___WIN32__" = 'yes'; then
		with_mutex='WIN32_API'
	fi

	# When unknown, prefer POSIX standard over SystemV.
	if test "$with_mutex" = 'UNKNOWN_API'; then
		if test ${ac_cv_func_sem_init:-no} = 'yes'; then
			with_mutex='POSIX_API'
		elif test $snert_systemv_semaphores = 'yes'; then
			with_mutex='SYSTEMV_API'
		elif test $has_mutex_fcntl_api = 'yes'; then
			with_mutex='FCNTL_API'
		elif test $has_mutex_flock_api = 'yes'; then
			with_mutex='FLOCK_API'
		fi
	fi
fi

AC_DEFINE_UNQUOTED(SERIALIZATION_API, $with_mutex)
AC_SUBST(SERIALIZATION_API, "$with_mutex")
AC_MSG_RESULT($with_mutex)

#######################################################################
#	Determine avaliable Shared Memory APIs
#######################################################################

has_shm_mmap_api='yes'
AC_CHECK_FILE(/dev/zero)
SNERT_CHECK_DEFINE(MMAP_ANON, sys/mman.h)
AC_CHECK_FUNCS([mmap munmap], [], [has_shm_mmap_api='no'])
if test "$has_shm_mmap_api" = 'yes'; then
	if test "$ac_cv_define_MMAP_ANON" = 'yes'; then
		has_shm_mmap_api='anon'
	elif test "$ac_cv_file__dev_zero" = 'no'; then
		has_shm_mmap_api='no'
	fi
fi

has_shm_systemv_api='yes'
AC_CHECK_FUNCS([shmget shmat shmctl shmdt], [], [has_shm_systemv_api='no'])

if test "$ac_cv_header_windows_h" = 'yes'; then
	has_shm_win32_api='yes'
else
	has_shm_win32_api='no'
fi

dnl Shared memory API
dnl APR_CHECK_FILE(/dev/zero)
dnl
dnl if test $with_shar_mem = 'UNKNOWN_API'; then
dnl 	with_shar_mem='POSIX_API'
dnl 	SNERT_CHECK_DEFINE(MMAP_ANON, sys/mman.h)
dnl 	AC_CHECK_FUNCS([mmap munmap], [], [with_shar_mem='UNKNOWN_API'])
dnl
dnl 	if test $with_shar_mem = 'POSIX_API'; then
dnl 		if test $ac_cv_define_MMAP_ANON = yes; then
dnl 			with_shar_mem='MMAP_ANON'
dnl 		elif test $ac_cv_file__dev_zero = no; then
dnl 			with_shar_mem='UNKNOWN_API'
dnl 		fi
dnl 	fi
dnl fi
dnl if test $with_shar_mem = 'UNKNOWN_API'; then
dnl 	with_shar_mem='SYSTEMV_API'
dnl 	AC_CHECK_FUNCS([shmget shmat shmctl shmdt], [], [with_shar_mem='UNKNOWN_API'])
dnl fi
dnl if test $with_shar_mem = 'UNKNOWN_API'; then
dnl 	if test $ac_cv_header_windows_h = yes; then
dnl 		with_shar_mem='MALLOC_API'
dnl 	fi
dnl fi

AC_MSG_CHECKING([which shared memory api to use])

if test "$with_shar_mem" = 'UNKNOWN_API'; then
	# Out in right field...
	if test "$ac_cv_define___WIN32__" = 'yes'; then
		with_shar_mem='WIN32_API'
	fi

	# When unknown, prefer POSIX standard over others.
	if test "$with_shar_mem" = 'UNKNOWN_API'; then
		if test "$has_shm_mmap_api" = 'yes'; then
			with_shar_mem='POSIX_API'
		elif test "$has_shm_mmap_api" = 'anon'; then
			with_shar_mem='MMAP_ANON'
		elif test "$has_shm_systemv_api" = 'yes'; then
			with_shar_mem='SYSTEMV_API'
		fi
	fi
fi

AC_DEFINE_UNQUOTED(SHARED_MEMORY_API, $with_shar_mem)
AC_SUBST(SHARED_MEMORY_API, "$with_shar_mem")
AC_MSG_RESULT($with_shar_mem)

#######################################################################
#	Generate output.
#######################################################################

SNERT_FINI

echo
echo "Generating files..."
echo
AC_OUTPUT()

echo
echo $PACKAGE_NAME/$package_major.$package_minor.$package_build
echo $package_copyright
echo
AC_MSG_RESULT([  Platform............: $platform $CC])
AC_MSG_RESULT([  prefix..............: $prefix])
AC_MSG_RESULT([  datarootdir.........: $datarootdir])
AC_MSG_RESULT([  Berkeley DB.........: $bdb_version "$CFLAGS_DB" "$LDFLAGS_DB" "${HAVE_LIB_DB}"])
AC_MSG_RESULT([  Lua.................: "$CFLAGS_LUA" "$LDFLAGS_LUA" "$HAVE_LIB_LUA"])
AC_MSG_RESULT([  libev...............: "$CFLAGS_LIBEV" "$LDFLAGS_LIBEV" "$HAVE_LIB_LIBEV"])
AC_MSG_RESULT([  OpenSSL.............: "$CFLAGS_SSL" "$LDFLAGS_SSL" "$HAVE_LIBSSL $HAVE_LIBCRYPTO"])
AC_MSG_RESULT([  POSIX Semaphores....: "" "" "$HAVE_LIB_SEM"])
AC_MSG_RESULT([  POSIX Threads.......: "$CFLAGS_PTHREAD" "$LDFLAGS_PTHREAD" "$HAVE_LIB_PTHREAD"])
AC_MSG_RESULT([  Sendmail libmilter..: "$CFLAGS_MILTER" "$LDFLAGS_MILTER" "$HAVE_LIB_MILTER"])
AC_MSG_RESULT([  SQLite3.............: "$CFLAGS_SQLITE3" "$LDFLAGS_SQLITE3" "$HAVE_LIB_SQLITE3"])
AC_MSG_RESULT([  Semaphore API.......: $with_mutex])
AC_MSG_RESULT([  Shared Memory API...: $with_shar_mem])
AC_MSG_RESULT([  CFLAGS..............: $CFLAGS])
AC_MSG_RESULT([  LDFLAGS.............: $LDFLAGS])
AC_MSG_RESULT([  LIBS................: $LIBS])
AC_MSG_RESULT([  Network LIBS........: $NETWORK_LIBS])
echo

#######################################################################
#	-END-
#######################################################################
